apply plugin: 'org.jmailen.kotlinter'
apply plugin: 'com.adarshr.test-logger'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"
    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        versionCode 1
        versionName "1.0.0"
        minSdkVersion 23
        targetSdkVersion 30
        vectorDrawables {
            useSupportLibrary true
            generatedDensities = []
        }
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        debug {
            crunchPngs false
        }

        release {
            minifyEnabled true
            shrinkResources true

            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                getDefaultProguardFile("proguard-android.txt"),
                "proguard-rules.pro"
            )
            proguardFiles(rootProject.file("proguard").listFiles())

            // Will be null if has not a release signing
            signingConfig = signingConfigs.findByName("release")
        }
    }

    flavorDimensions "default"

    productFlavors {
        dev {
            dimension "default"
            applicationIdSuffix ".dev"
            resConfigs(
                "en-rUS", // Language and region
                "ldltr", // Layout Direction
                "port", // Screen orientation
                "xxxhdpi" // Screen pixel density (dpi)
            )
        }
        prd {
            dimension "default"
        }
    }

    packagingOptions {
        exclude "LICENSE.txt"
        exclude "META-INF/DEPENDENCIES"
        exclude "META-INF/ASL2.0"
        exclude "META-INF/NOTICE"
        exclude "META-INF/LICENSE"
        exclude "META-INF/main.kotlin_module"
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError true
        ignoreWarnings true
        quiet false
        disable "InvalidPackage"
        disable "OldTargetApi"
    }

    testOptions {
        unitTests {
            includeAndroidResources true
            returnDefaultValues true
        }
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/kotlin']
        }
    }
}

lintKotlinMain {
    exclude buildDir.path
}

testlogger {
    theme 'mocha'
}
